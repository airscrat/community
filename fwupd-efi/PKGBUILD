# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=fwupd-efi
pkgver=1.3
pkgrel=1
pkgdesc='EFI Application used by uefi-capsule plugin in fwupd'
arch=('loong64' 'x86_64')
url='https://github.com/fwupd/fwupd-efi'
license=('LGPL')
makedepends=('meson' 'gnu-efi')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc}
        0001-add-support-for-loongarch64.patch)
sha256sums=('d58b34e13c2159867c391260af4e2418eebf948d48dfab02ea472b5683ffaf59'
            'SKIP'
            '01819145bad4c0bd945e230ec3d7fe044c1806a80178f7c1f3d0a3bc07f9def5')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

prepare() {
	cd "$pkgname-$pkgver"
	patch -p1 -i "$srcdir/0001-add-support-for-loongarch64.patch"
}

build() {
  arch-meson ${pkgname}-${pkgver} build  \
    -D efi_sbat_distro_id='arch' \
    -D efi_sbat_distro_summary='Arch Linux' \
    -D efi_sbat_distro_pkgname=${pkgname} \
    -D efi_sbat_distro_version=${pkgver} \
    -D efi_sbat_distro_url="https://archlinux.org/packages/community/loongarch64/${pkgname}/"
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
