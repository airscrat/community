# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=acpica
pkgver=20220331
pkgrel=1
pkgdesc="ACPI tools, including Intel ACPI Source Language compiler"
arch=(loong64 x86_64)
url="https://acpica.org"
license=(GPL2)
depends=(glibc)
makedepends=(flex bison)
provides=(iasl)
conflicts=(iasl)
replaces=(iasl)
options=(!makeflags)
source=(${pkgname}-unix-${pkgver}.tar.gz::https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz
        acpica-fix-build.patch
        acpica-unix-la64.patch)
sha256sums=('acaff68b14f1e0804ebbfc4b97268a4ccbefcfa053b02ed9924f2b14d8a98e21'
            '6087d8cb862b2036177a5620a1fd909153eace771c6776c5c2330f8eebb90f12'
            '7d0f28c3532638ead8696d839589464ebce49a15f76ca892b96ef442c3a08d1c')

prepare() {
  cd acpica-unix-${pkgver}
  patch -p1 -i "$srcdir/acpica-fix-build.patch"
  patch -p1 -i "$srcdir/$pkgname-unix-la64.patch"
}

build() {
  cd acpica-unix-${pkgver}
  make
}

package() {
  cd acpica-unix-${pkgver}
  make DESTDIR="${pkgdir}" install
}
