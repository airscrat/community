# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-greenlet
pkgver=2.0.0
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=('loong64' 'x86_64')
url="https://pypi.org/project/greenlet/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-objgraph')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz"
        python-greenlet-la64.patch)
sha512sums=('2c4ae0623e8258a85012fca3c4c419cab3634679dc05a51b9079bcae3afb6f6c3c5052b249dbdb48a8dac3d20b9313343b217862ab34f3ae6dd5cda66e53dc35'
            'a427f6121802409a0d5d83a44eeafa50e19e5b1a02041220b2ee98f90d262e4acc0affc8d7f9ae5bbec05a738293dbf5c6d37adca0291ab753c0f41f7a7d5c2a')

prepare() {
	cd greenlet-$pkgver
    patch -p1 -i $srcdir/python-greenlet-la64.patch
}

build() {
	cd greenlet-$pkgver
	python setup.py build
}

check() {
    cd greenlet-$pkgver
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310" python -m unittest discover -v greenlet.tests
}

package() {
    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm644 LICENSE.PSF -t "$pkgdir"/usr/share/licenses/$pkgname/
}
