# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=20220226175453
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2fly/domain-list-community"
license=('MIT')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2fly/domain-list-community/archive/$pkgver.tar.gz")
sha512sums=('10f7d072c9d909e0f24c7251cf516099977647177078ddc695ce2882bb9ce1c0c082a6fef76ed1e6689e888e94c5654e68664563866810e62049fff3990e85f2')

build() {
  cd domain-list-community-$pkgver
  go run main.go
}

package() {
  cd domain-list-community-$pkgver
  install -Dm644 dlc.dat "$pkgdir"/usr/share/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
