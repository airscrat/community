# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=clash-lib
pkgname=haskell-clash-lib
pkgver=1.4.7
pkgrel=2
pkgdesc="Clash: a functional hardware description language - As a library"
url="https://clash-lang.org/"
license=("BSD")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-ansi-terminal'
         'haskell-attoparsec' 'haskell-base16-bytestring' 'haskell-clash-prelude'
         'haskell-concurrent-supply' 'haskell-cryptohash-sha256' 'haskell-data-binary-ieee754'
         'haskell-data-default' 'haskell-dlist' 'haskell-errors' 'haskell-extra' 'haskell-ghc'
         'haskell-hashable' 'haskell-src-meta' 'haskell-hint' 'haskell-interpolate' 'haskell-lens'
         'haskell-ordered-containers' 'haskell-parsers' 'haskell-pretty-show'
         'haskell-prettyprinter' 'haskell-primitive' 'haskell-reducers' 'haskell-temporary'
         'haskell-terminal-size' 'haskell-text-show' 'haskell-trifecta'
         'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-vector'
         'haskell-vector-binary-instances')
makedepends=('ghc' 'uusi' 'haskell-ghc-typelits-knownnat' 'haskell-quickcheck-text' 'haskell-src-exts'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('587ab6f6d11289bbf0d3ad7da9b425316758e0c093186638afe8fe562af7534c679b654274670b854e0c2a8ae444992ac3d65d791c2cb94c48cec3ea3c730416')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u ghc-bignum $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
