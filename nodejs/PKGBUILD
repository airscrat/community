# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=18.8.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('loong64' 'x86_64')
url='https://nodejs.org/'
license=('MIT')
options=(!lto)
depends=('brotli' 'openssl' 'zlib' 'icu' 'libuv' 'libnghttp2' 'c-ares') # 'http-parser' 'v8')
makedepends=('python' 'procps-ng')
optdepends=('npm: nodejs package manager')
source=("https://github.com/nodejs/node/archive/v$pkgver/nodejs-$pkgver.tar.gz"
    nodejs-gcc12-build.patch)
sha512sums=('d8793aec2fba3d4e2747557c1a457c33c37f1e7ee86988c0e22ec844b254675c39ccaad9908fb63f7814fe759d94d85f18460e74b368d6e75b545f52462891ca'
            '8092d4f36bc098874dee25c3f00a1dfede4989fe640d014e551faff44ede5e5766c643dc26ebe302640ced4f78fb784f816e9fce77888e4e126a186287084dcb')

prepare() {
	cd "node-v$pkgver"
	patch -p1 -i "$srcdir/nodejs-gcc12-build.patch"
}

build() {
  cd node-v$pkgver

  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-nghttp2 \
    --shared-cares \
    --shared-brotli
    # --shared-v8
    # --shared-http-parser

  make
}

check() {
  cd node-v$pkgver
  make test || :
}

package() {
  cd node-v$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
