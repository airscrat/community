# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-ujson
_pkgname=ultrajson
pkgver=5.4.0
pkgrel=1
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/ultrajson/ultrajson"
arch=('loong64' 'x86_64')
license=('BSD')
depends=('python')
makedepends=(python-setuptools-scm python-wheel python-build python-installer)
checkdepends=('python-six' 'python-pytest')
source=(https://github.com/ultrajson/ultrajson/archive/$pkgver/$pkgname-$pkgver.tar.gz
  python-ultrajson-la64.patch)
sha512sums=('912aa5ed031537da15b6cd6ee3b432cd2729c9cef3de948b342b3a22617c109fed7c64fd0f9231271f45e54efef088d02d9fd177aaa221d4e89ac9e7777e17ec'
            '6b72a18c5e1d3e7f48fc52651898607d75131edd1c2a731eb1afa3062a545d6d00a14210a41b9683ff68813722b60078640cca9d2cc6d37f5c3e862c301c5999')
b2sums=('bd182fb5da034fa9e64d48a882ca14a23856a263af5aefe111fa3f27d4b2a139336d3ab2f4e4c25da7b3dcdf81b2d42a79deda759c27f8645c980e1f99e2face'
        '5e5e9c4712b2da1ee08b7c70cdabad254574161d53d78a1a6965a8e12273ba992b0461bdc4dbd2b4f27d6815242ba595a42ec4fc868e99e15f763b3799365ca9')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -p1 -i $srcdir/python-ultrajson-la64.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" pytest
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
