# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-ujson
_pkgname=ultrajson
pkgver=5.5.0
pkgrel=1
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/ultrajson/ultrajson"
arch=('loong64' 'x86_64')
license=('BSD')
depends=('python')
makedepends=(python-setuptools-scm python-wheel python-build python-installer)
checkdepends=('python-six' 'python-pytest')
source=(https://github.com/ultrajson/ultrajson/archive/$pkgver/$pkgname-$pkgver.tar.gz
        python-ultrajson-la64.patch)
sha512sums=('bd4336010066af2811b661ef64f3c23436baa6af91ffee1c4bf657116ded9e154fc2a98b797d3048dba8038d9dda0ae9cea2303d72be6e192dc815adb8e82a81'
            '6b72a18c5e1d3e7f48fc52651898607d75131edd1c2a731eb1afa3062a545d6d00a14210a41b9683ff68813722b60078640cca9d2cc6d37f5c3e862c301c5999')
b2sums=('ff632acf207999a860ca3070329b3843c941933b34694b8e634c3e01de84151de7e3cce610dd3d4529aab7638c7ed9c58db8da379701a9ca401991be2782dd71'
        '5e5e9c4712b2da1ee08b7c70cdabad254574161d53d78a1a6965a8e12273ba992b0461bdc4dbd2b4f27d6815242ba595a42ec4fc868e99e15f763b3799365ca9')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -p1 -i $srcdir/python-ultrajson-la64.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  local PYTHONVERSION="$(python -c 'import sys; print("{}{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="build/lib.linux-${CARCH}-cpython-${PYTHONVERSION}" pytest
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
